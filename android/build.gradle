apply plugin: "com.android.application"


android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.github.st1hy.sabre"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.version
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }

    dexOptions {
        maxProcessCount = 4
    }


}

configurations {
    natives
}

repositories {
    maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    maven { url "http://dl.bintray.com/st1hy/maven" }
}

dependencies {
    compile project(':core')
    compile project(':imagecache')
    compile project(':dao-database')
    compile "com.android.support:recyclerview-v7:$rootProject.ext.supportLibraryVersion"
    compile "com.android.support:appcompat-v7:$rootProject.ext.supportLibraryVersion"
    compile "com.android.support:cardview-v7:$rootProject.ext.supportLibraryVersion"
    compile "com.android.support:design:$rootProject.ext.supportLibraryVersion"
    compile 'org.st1hy.android:gesture-detector:1.0.3@aar'
    compile 'com.github.rey5137:material:1.2.2'
    compile 'com.balysv:material-ripple:1.0.2'
    compile 'com.jakewharton:butterknife:7.0.1'

    compile "com.badlogicgames.gdx:gdx-backend-android:$rootProject.ext.gdxVersion"
    natives "com.badlogicgames.gdx:gdx-platform:$rootProject.ext.gdxVersion:natives-armeabi"
    natives "com.badlogicgames.gdx:gdx-platform:$rootProject.ext.gdxVersion:natives-armeabi-v7a"
    natives "com.badlogicgames.gdx:gdx-platform:$rootProject.ext.gdxVersion:natives-arm64-v8a"
    natives "com.badlogicgames.gdx:gdx-platform:$rootProject.ext.gdxVersion:natives-x86"
    natives "com.badlogicgames.gdx:gdx-platform:$rootProject.ext.gdxVersion:natives-x86_64"
    compile "com.badlogicgames.gdx:gdx-box2d:$rootProject.ext.gdxVersion"
    natives "com.badlogicgames.gdx:gdx-box2d-platform:$rootProject.ext.gdxVersion:natives-armeabi"
    natives "com.badlogicgames.gdx:gdx-box2d-platform:$rootProject.ext.gdxVersion:natives-armeabi-v7a"
    natives "com.badlogicgames.gdx:gdx-box2d-platform:$rootProject.ext.gdxVersion:natives-arm64-v8a"
    natives "com.badlogicgames.gdx:gdx-box2d-platform:$rootProject.ext.gdxVersion:natives-x86"
    natives "com.badlogicgames.gdx:gdx-box2d-platform:$rootProject.ext.gdxVersion:natives-x86_64"
}


// called every time gradle gets executed, takes the native dependencies of
// the natives configuration, and extracts them to the proper libs/ folders
// so they get packed with the APK.
task copyAndroidNatives() {
    file("libs/armeabi/").mkdirs();
    file("libs/armeabi-v7a/").mkdirs();
    file("libs/arm64-v8a/").mkdirs();
    file("libs/x86_64/").mkdirs();
    file("libs/x86/").mkdirs();

    configurations.natives.files.each { jar ->
        def outputDir = null
        if(jar.name.endsWith("natives-arm64-v8a.jar")) outputDir = file("libs/arm64-v8a")
        if(jar.name.endsWith("natives-armeabi-v7a.jar")) outputDir = file("libs/armeabi-v7a")
        if(jar.name.endsWith("natives-armeabi.jar")) outputDir = file("libs/armeabi")
        if(jar.name.endsWith("natives-x86_64.jar")) outputDir = file("libs/x86_64")
        if(jar.name.endsWith("natives-x86.jar")) outputDir = file("libs/x86")
        if(outputDir != null) {
            copy {
                from zipTree(jar)
                into outputDir
                include "*.so"
            }
        }
    }
}